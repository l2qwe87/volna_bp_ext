
&После("ДобавитьКомандыПечати")
Процедура влн_ДобавитьКомандыПечати(КомандыПечати)
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетаОрганизации";
	КомандаПечати.Представление = "Счета организации";
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаЭлемента";
КонецПроцедуры


&НаСервере
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетаОрганизации") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СчетаОрганизации", "Счета организации",
			СформироватьПФ_СчетаОрганизацииПоЗаказам(МассивОбъектов)
		);
	КонецЕсли;
КонецПроцедуры 


Функция СформироватьПФ_СчетаОрганизацииПоЗаказам(МассивОбъектов)
	ТабДокумент = Новый ТабличныйДокумент;
	Для каждого Ссылка из МассивОбъектов Цикл
		ТабДокументОбъекта = СформироватьПФ_СчетаОрганизацииПоЗаказу(Ссылка);
		Если ТабДокумент.ВысотаТаблицы > 0 И ТабДокументОбъекта.ВысотаТаблицы > 0 Тогда 
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц()
		КонецЕсли;
		ТабДокумент.Вывести(ТабДокументОбъекта)
	КонецЦикла;
	
	ТабДокумент.ТолькоПросмотр = Истина;
	Возврат ТабДокумент
КонецФункции

Функция  СформироватьПФ_СчетаОрганизацииПоЗаказу(Ссылка)
	ТабДокумент = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Т.Значение КАК ИГК,
	               |	Счета.Объект КАК БанковскийСчет,
	               |	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
	               |	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт,
	               |	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток

	               |ИЗ
	               |	Справочник.НоменклатурныеГруппы.ДополнительныеРеквизиты КАК Т
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК Счета
	               |		ПО (Счета.Свойство.Заголовок = ""Уникальный идентификатор контракта"")
	               |			И Т.Значение = Счета.Значение
				   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(, , Период, , Счет.Код = ""51"", , ) КАК ХозрасчетныйОстаткиИОбороты
	               |			ПО Счета.Объект = ХозрасчетныйОстаткиИОбороты.Субконто1
	               |ГДЕ
	               |	Т.Свойство.Заголовок = ""Идентификатор государственного контракта""
	               |	И Т.Ссылка = &Ссылка
	               |ИТОГИ ПО
	               |	ИГК";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	макет = ПолучитьМакет("влн_Макет");
		
	РезИГК = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	РезИГК.Следующий();
	
	областьШапка = макет.ПолучитьОбласть("Шапка");
	областьШапка.Параметры.НоменклатурнаяГруппа = Ссылка;
	областьШапка.Параметры.Заполнить(РезИГК);
	ТабДокумент.Вывести(областьШапка);
	
	Если НЕ ЗначениеЗаполнено(РезИГК.ИГК) Тогда
		Сообщить("В заказе ИГК не заполнен");
	Иначе
		Рез = РезИГК.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		Пока Рез.Следующий() Цикл
			областьДанные = макет.ПолучитьОбласть("Данные");
			областьДанные.Параметры.Заполнить(Рез);
			ТабДокумент.Вывести(областьДанные);
		КонецЦикла;
	КонецЕсли;	
	
	Возврат ТабДокумент
КонецФункции 